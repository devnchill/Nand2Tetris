/*
 * ALU (Arithmetic Logic Unit):
 * Computes out = one of the following functions:
 *                0, 1, -1,
 *                x, y, !x, !y, -x, -y,
 *                x + 1, y + 1, x - 1, y - 1,
 *                x + y, x - y, y - x,
 *                x & y, x | y
 * on the 16-bit inputs x, y,
 * according to the input bits zx, nx, zy, ny, f, no.
 * In addition, computes the two output bits:
 * if (out == 0) zr = 1, else zr = 0
 * if (out < 0)  ng = 1, else ng = 0
 * Implementation: Manipulates the x and y inputs
 * and operates on the resulting values, as follows:
 * if (zx == 1) sets x = 0        // 16-bit constant
 * if (nx == 1) sets x = !x       // bitwise not
 * if (zy == 1) sets y = 0        // 16-bit constant
 * if (ny == 1) sets y = !y       // bitwise not
 * if (f == 0)  sets out = x & y  // bitwise and
 * if (f == 1)  sets out = x + y  // integer 2's complement addition
 * if (no == 1) sets out = !out   // bitwise not
 */

CHIP ALU {
    IN
        x[16], y[16],  -- 16-bit inputs        
        zx,
        nx,
        zy,
        ny,
        f,
        no;
    OUT
        out[16],
        zr,      -- if (out == 0) equals 1, else 0
        ng;      -- if (out < 0)  equals 1, else 0

    PARTS:
    Mux16(a=x,b=false,sel=zx,out=out1);
    Not16(in=out1,out=notout1);
    Mux16(a=out1,b=notout1,sel=nx,out=out2);
    Mux16(a=y,b=false,sel=zy,out=out3);
    Not16(in=out3,out=notout2);
    Mux16(a=out3,b=notout2,sel=ny,out=out4);
    And16(a=out2,b=out4,out=xandy);
    Add16(a=out2,b=out4,out=xplusy);
    Mux16(a=xandy,b=xplusy,sel=f,out=out5);
    Not16(in=out5,out=notout5);
    Mux16(a=out5,b=notout5,sel=no,out=result,out[15]=ng, out[0..7]=zout1, out[8..15]=zout2);
    Or8Way(in=zout1,out=anybitset1);
    Or8Way(in=zout2,out=anybitset2);
    Or(a=anybitset1,b=anybitset2,out=anybitsetfinal);
    Not(in=anybitsetfinal,out=zr);
    Or16(a=result,b=false,out=out);
}
